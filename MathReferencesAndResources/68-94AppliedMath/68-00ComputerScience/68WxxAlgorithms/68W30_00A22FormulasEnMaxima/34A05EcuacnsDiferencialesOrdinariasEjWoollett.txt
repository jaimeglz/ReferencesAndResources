/* Ejemplos tomados de Woollett, Maxima by Example, Ch. 3 */

	de : 'diff(u,t) - u - %e^-t;

/*
                                du         - t
(%o2)                           -- - u - %e
                                dt
*/

	gsoln : ode2(de,u,t);

/*                                        - 2 t
                                      %e         t
(%o3)                       u = (%c - -------) %e
                                         2
*/

	psoln : ic1(gsoln, t=2, u=-0.1),raprint=false;

/*
                           - t - 4     2        2 t       4
                         %e        ((%e  - 5) %e    + 5 %e )
(%o4)              u = - -----------------------------------
                                         10
*/

	rhs(psoln),t=2,ratsimp;

/*
                                       1
(%o5)                                - --
                                       10
*/

	de,psoln,diff,ratsimp;

/*
(%o6)                                  0
*/

	us : rhs(psoln);

/*
                         - t - 4     2        2 t       4
                       %e        ((%e  - 5) %e    + 5 %e )
(%o7)                - -----------------------------------
                                       10
*/

	plot2d(us,[t,0,7],
		[style,[lines,5]],[ylabel," "],
		[xlabel,"t0 = 2, u0 = -0.1, du/dt = exp(-t) + u"])$

/*	Woollett: Exact Solution Using desolve    */

	eqn : 'diff(u(t),t) - exp(-t) - u(t) = 0;

/*
                         d                    - t
(%o9)                    -- (u(t)) - u(t) - %e    = 0
                         dt
*/

	gsoln : desolve(eqn,u(t));

/*
                                              t     - t
                               (2 u(0) + 1) %e    %e
(%o10)                  u(t) = ---------------- - -----
                                      2             2
*/

	eqn,gsoln,diff,ratsimp;

/*
(%o11)                               0 = 0
*/

	bc : subst ( t=2, rhs(gsoln)) = -0.1;

/*	Diagramación con plotdf 
	Formato general:
		plotdf( dudt,[t,u], [trajectory_at, t0, u0], options ... )
*/

	plotdf(exp(-t) + u, [t, u], [trajectory_at, 2, -0.1],
		[direction, forward], [t,0,7], [u, -6, 1] )$

/*	Desarrollo de ejemplos sencillos	*/

	plotdf(1/2 * x^2, [trajectory_at, 5, 5] );

	plotdf(x, [trajectory_at, 2, 2] );

	plotdf((x^2-y^2) / (1 + x^2+y^2), [trajectory_at, 0, -4] );